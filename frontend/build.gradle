plugins {
    id 'com.github.node-gradle.node' version '7.0.1'
    id 'base'
}

node {
    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.buildDir}/npm")
}

tasks.register('appNpmInstall', NpmTask) {
    description = "Installs all dependencies from package.json"

    inputs.file('package.json')
    outputs.dir('node_modules')

    args = ["install"]
}

tasks.register('appNpmBuild', NpmTask) {
    description = "Builds production version of the webapp"

    inputs.files(fileTree('node_modules'))
    inputs.files(fileTree('src'))
    inputs.file('package.json')
    inputs.file('tsconfig.json')
    inputs.file('tsconfig.node.json')
    inputs.file('vite.config.ts')
    inputs.file('index.html')

    outputs.dir('build')

    args = ["run", "build"]
}

appNpmBuild.dependsOn(appNpmInstall)
assemble.dependsOn(appNpmBuild)
